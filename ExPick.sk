#==================#
#  Hardcore OptIn  #
#      v1.4.1      #
#   Arctic Raven   #
#    KingFrosty    #
#==================#

options:
	
	#The death system allows the choice of what happens to a player who has opted into hardcore when they die
	#option 1: "spectate" - this option allows users to exist in spectator after death
	#option 2: "tempban" - this option allows users to rejoin the server after configurable amount of time
	#option 3: "ban" - this option fully bans players from the server on death
	DeathSystem : "spectate"
	
	#this option sets increment in which the player is not allowed to rejoin
	#options: years, months, weeks, days, hours, minutes, seconds (recommend days or weeks)
	BanIncrement : days
	
	#this option sets the numerical value for the increments, ie 1 "days", 5 "months" etc. 
	BanTime : 1
	
	#this Option sets hardcore by default for new players. False by default 
	DefaultHardcore: false

on join:
	set {hardcore::%uuid of player%} to true if {@DefaultHardcore} is true

command Hardcore [<text="false">]:
	description: Opt In to playing simulated hardcore on this server
	usage: hardcore <arguement>
	trigger:
		if arg-1 is "true":
			set {hardcore::confirmation-time::%uuid of player%} to now
			message "&7[&4Hardcore&7] &4Please execute '/hardcore confirm' to confirm"
		if arg-1 is "confirm":
			if {hardcore::confirmation-time::%uuid of player%} is set:
				set {_time} to difference between {hardcore::confirmation-time::%uuid of player%} and now
				if {_time} is more than 30 seconds:
					message "&7[&4Hardcore&7] &cHardcore mode &4has expired, type '/hardcore true' again."
					delete {hardcore::confirmation-time::%uuid of player%}
					stop
				set {hardcore::confirmed::%uuid of player%} to true
				message "&7[&4Hardcore&7] &cHardcore mode &aconfirmed"
		if arg-1 is "quit":
			if player has permission "hardcore.quit":
				delete {hardcore::%uuid of player%}
				delete {hardcore::confirmation-time::%uuid of player%}
				message "&7[&4Hardcore&7] &cHardcore mode deactivated, have a nice game!"
				set player's gamemode to survival
		if arg-1 is not "confirm" or "true" or "quit":
			message "&7[&4Hardcore&7] &cIncorrect usage, please use /hardcore true to start the hardcore Opt In process"


on death of player:
	if {hardcore::confirmed::%uuid of player%} is set:
		if {@DeathSystem} is "spectate":
			set player's gamemode to spectator
		if {@DeathSystem} is "tempban":
			kick player
			ban player for {@BanTime} {@BanIncrement}
		if {@DeathSystem} is "ban":
			kick player
			ban player due to "Death in Hardcore"
